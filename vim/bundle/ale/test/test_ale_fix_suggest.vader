Before:
  call ale#fix#registry#Clear()

  function GetSuggestions()
    redir => l:output
      silent ALEFixSuggest
    redir END

    return split(l:output, "\n")
  endfunction

After:
  call ale#fix#registry#ResetToDefaults()
  delfunction GetSuggestions

Execute(ALEFixSuggest should return something sensible with no suggestions):
  AssertEqual
  \ [
  \   'There is nothing in the registry to suggest.',
  \ ],
  \ GetSuggestions()

Execute(ALEFixSuggest output should be correct for only generic handlers):
  call ale#fix#registry#Add('zed', 'XYZ', [], 'Zedify things.')
  call ale#fix#registry#Add('alpha', 'XYZ', [], 'Alpha things.')

  AssertEqual
  \ [
  \   'Try the following generic fixers:',
  \   '',
  \   '''alpha'' - Alpha things.',
  \   '''zed'' - Zedify things.',
  \   '',
  \   'See :help ale-fix-configuration',
  \ ],
  \ GetSuggestions()

Execute(ALEFixSuggest output should be correct for only filetype handlers):
  let &filetype = 'testft2.testft'

  call ale#fix#registry#Add('zed', 'XYZ', ['testft2'], 'Zedify things.')
  call ale#fix#registry#Add('alpha', 'XYZ', ['testft'], 'Alpha things.')

  AssertEqual
  \ [
  \   'Try the following fixers appropriate for the filetype:',
  \   '',
  \   '''alpha'' - Alpha things.',
  \   '''zed'' - Zedify things.',
  \   '',
  \   'See :help ale-fix-configuration',
  \ ],
  \ GetSuggestions()

Execute(ALEFixSuggest should suggest filetype and generic handlers):
  let &filetype = 'testft2.testft'

  call ale#fix#registry#Add('zed', 'XYZ', ['testft2'], 'Zedify things.')
  call ale#fix#registry#Add('alpha', 'XYZ', ['testft'], 'Alpha things.')
  call ale#fix#registry#Add('generic', 'XYZ', [], 'Generic things.')

  AssertEqual
  \ [
  \   'Try the following fixers appropriate for the filetype:',
  \   '',
  \   '''alpha'' - Alpha things.',
  \   '''zed'' - Zedify things.',
  \   '',
  \   'Try the following generic fixers:',
  \   '',
  \   '''generic'' - Generic things.',
  \   '',
  \   'See :help ale-fix-configuration',
  \ ],
  \ GetSuggestions()
