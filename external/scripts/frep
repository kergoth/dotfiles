#!/bin/sh
# frep -- filter field repetitions
#
# Usage: frep [-Ffs] [-l] FIELDS [--] FILES
#   -F sets field seperator (like in awk)
#   -l will print the last, not first, occurence of each repeated field.

exec awk '
BEGIN {
  last = 0
  delete fields[0]
  ll = -1 
  for (i=1; i<ARGC; i++) {
    if (ARGV[i] == "-l") last = 1
    else if (ARGV[i] ~ /^-F/) FS = substr(ARGV[i], 3)
    else if (ARGV[i] ~ /^[0-9]+$/) fields[length(fields)+1] = ARGV[i]
    else if (ARGV[i] == "--") { delete ARGV[i]; break }
    else break
    delete ARGV[i]
  }
}
{ 
  nk = ""
  for (f in fields) nk = nk SUBSEP $fields[f]
  if (last) { if (k != nk && ll != -1) print ll }
  else { if (k != nk) print $0 }
  k = nk
  ll = $0
}
END { if (last && ll != -1) print ll }
' "$@"
