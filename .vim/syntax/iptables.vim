"============================================================================
"
" iptables-save/restore syntax highlighter
"
" Language:	   iptables-save/restore file
" Version:     1.02
" Date:        22-Dec-2005
" Maintainer:  Eric Haarbauer <ehaar{DOT}com{AT}grithix{DOT}dyndns{DOT}org>
" License:     This file is placed in the public domain.
"
"============================================================================
" Section:  Notes  {{{1
"============================================================================
"
" This vim syntax script highlights files used by Harald Welte's iptables-save
" and iptables-restore utilities.  Both utilities are part of the iptables
" application (http://www.netfilter.org/projects/iptables).
" 
" Features:
"
"   * Distinguishes commands, options, modules, targets and chains.
"   * Distinguishes numeric IP addresses from net masks.
"   * Highlights tokens that occur only in hand-edited files; for example,
"     "--append" and "destination-unreachable".
"   * Special handling for module names; for example, the tcp module is
"     colored differently from the tcp protocol.
"
" Options:
"
"   To distinguish numeric delimiters, including the dots in IP addresses, the
"   slash that separates an IP address from a netmask, and the colon that
"   separates the ends of a port range, place the following line in your
"   .vimrc file:
"
"      let g:iptablesSpecialDelimiters = 1
"
"   By default, this option is turned off.
"
" Known Issues:
"
"   * Some special argument tokens get highlighted whether or not they are
"     used with the correct option.  For example, "destination-unreachable"
"     gets special highlighting whether or not is used as an argument to the
"     --icmp-type option.
"
" Reporting Issues:
"
"   If you discover an iptables file that this script highlights incorrectly,
"   please email the author (address at the top of the file) with the
"   following information:
"
"     * Problem iptables file WITH ANY SENSITIVE INFORMATION REMOVED
"     * The release version of this script (see top of the file)
"     * If possible, a patch to fix the problem
"
" Design Notes:
"
"   Part of this script is autogenerated from the output of the iptables man
"   page.  The source code for this script is available from the author on
"   request (see email address at the top of the file).  It should build on
"   most Linux systems with iptables installed.
"
"   The build system that generates this script strips special CVS tokens
"   (like "Id:") so that CVS no longer recognizes them.  This allows users to
"   place this script in their own version control system without losing
"   information.  The author encourages other vim script developers to adopt a
"   similar approach in their own scripts.
"
" Installation:
"
"   Put this file in your user runtime syntax directory, usually ~/.vim/syntax
"   in *NIX or C:\Program Files\vim\vimfiles\syntax in Windows.
"
"   The iptables-save and iptables-restore applications do not specify a
"   naming standard for the files they use.  However, iptables-save places a
"   comment in the first line of its output.  Other applications, such as
"   Fedora Core's system-config-securitylevel use the iptables-save/restore
"   format, but with a different leading comment.  We can use these leading
"   comments to identify the filetype by placing the following code in the
"   scripts.vim file in your user runtime directory:
"   
"      if getline(1) =~ "^# Generated by iptables-save" ||
"       \ getline(1) =~ "^# Firewall configuration written by"
"          setfiletype iptables
"          set commentstring=#%s
"          finish
"      endif
"
"   Setting the commentstring option allows Meikel Brandmeyer's
"   EnhancedCommentify script (vimscript #23) to work with iptables files.
"
"============================================================================
" Source File: Id: iptables.src.vim,v 1.10 2005/12/21 13:15:17 ehaar Exp 
"============================================================================
" Section:  Initialization  {{{1
"============================================================================

" Global variable initialization

if !exists("g:iptablesSpecialDelimiters")
    let b:iptablesSpecialDelimiters = 0
else
    let b:iptablesSpecialDelimiters = g:iptablesSpecialDelimiters
endif

" For version 5.x: Clear all syntax items
" For version 6.x: Quit when a syntax file was already loaded
if !exists("main_syntax")
  if version < 600
    syntax clear
  elseif exists("b:current_syntax")
    finish
  endif
  let main_syntax = 'iptables'
endif

" Don't use standard HiLink, it will not work with included syntax files
if version < 508
  command! -nargs=+ IptablesHiLink highlight link <args>
else
  command! -nargs=+ IptablesHiLink highlight default link <args>
endif

syntax case match

if version < 600
    set iskeyword+=-
else
    setlocal iskeyword+=-
endif

"============================================================================
" Section:  Group Definitions  {{{1
"============================================================================

syntax keyword iptablesSaveDirective COMMIT
syntax match   iptablesSaveOperation "^[:*]"

syntax keyword iptablesTable filter nat mangle raw

syntax keyword iptablesTarget
    \ ACCEPT DROP QUEUE RETURN
    \ BALANCE CLASSIFY CLUSTERIP CONNTRACK DNAT DSCP ECN LOG MARK
    \ MASQUERADE MIRROR NETMAP NOTRACK REDIRECT REJECT ROUTE SET
    \ SNAT TCPMSS TOS TRACE TTL ULOG

syntax keyword iptablesBuiltinChain
    \ INPUT OUTPUT FORWARD PREROUTING POSTROUTING

syntax keyword iptablesCommand -A -D -I -R -L -F -Z -N -X -P -E
    \ --append --delete --insert --replace --list --flush --zero
    \ --new-chain --delete-chain --policy --rename-chain

syntax keyword iptablesParam   -p -s -d -j -i -o -f -c -t

syntax match iptablesOperator "\s\zs!\ze\s"

syntax keyword iptablesModuleName contained
    \ addrtype ah childlevel condition connmark
    \ conntrack dscp dstlimit ecn esp fuzzy helper icmp iprange length
    \ limit mac mark mport multiport nth owner physdev pkttype random
    \ realm set state tcp tcpmss time tos ttl udp unclean

syntax keyword iptablesModuleType
    \ UNSPEC UNICAST LOCAL BROADCAST ANYCAST
    \ MULTICAST BLACKHOLE UNREACHABLE PROHIBIT THROW NAT XRESOLVE
    \ INVALID ESTABLISHED NEW RELATED
    \ SYN ACK FIN RST URG PSH ALL NONE

" From --reject-with option
syntax keyword iptablesModuleType
    \ icmp-net-unreachable
    \ icmp-host-unreachable
    \ icmp-port-unreachable
    \ icmp-proto-unreachable
    \ icmp-net-prohibited
    \ icmp-host-prohibited
    \ icmp-admin-prohibited

" From --icmp-type option
syntax keyword iptablesModuleType
    \ any
    \ echo-reply
    \ destination-unreachable
    \    network-unreachable
    \    host-unreachable
    \    protocol-unreachable
    \    port-unreachable
    \    fragmentation-needed
    \    source-route-failed
    \    network-unknown
    \    host-unknown
    \    network-prohibited
    \    host-prohibited
    \    TOS-network-unreachable
    \    TOS-host-unreachable
    \    communication-prohibited
    \    host-precedence-violation
    \    precedence-cutoff
    \ source-quench
    \ redirect
    \    network-redirect
    \    host-redirect
    \    TOS-network-redirect
    \    TOS-host-redirect
    \ echo-request
    \ router-advertisement
    \ router-solicitation
    \ time-exceeded
    \    ttl-zero-during-transit
    \    ttl-zero-during-reassembly
    \ parameter-problem
    \    ip-header-bad
    \    required-option-missing
    \ timestamp-request
    \ timestamp-reply
    \ address-mask-request
    \ address-mask-reply

" If we used a keyword for this, port names would be colored the same
" as modules with the same name (e.g. tcp, udp, icmp).
syntax keyword iptablesParam -m --match skipwhite nextgroup=iptablesModuleName

if b:iptablesSpecialDelimiters == 1
    syntax match iptablesNumber    "\<[0-9./:]\+\>"
        \ contains=iptablesMask,iptablesDelimiter
    syntax match iptablesDelimiter "[./:]"     contained
    syntax match iptablesMask      "/[0-9.]\+" contained 
        \ contains=iptablesDelimiter
else " b:iptablesSpecialDelimiters == 0
    syntax match iptablesNumber    "\<[0-9./]\+\>"
        \ contains=iptablesMask,iptablesDelimiter
    syntax match iptablesDelimiter "/"         contained
    syntax match iptablesMask      "/[0-9.]\+" contained 
        \ contains=iptablesDelimiter
endif

syntax region iptablesString start=+"+ skip=+\\"+ end=+"+ oneline

syntax match  iptablesComment    "^#.*" contains=iptablesTodo
syntax match  iptablesBadComment "^\s\+\zs#.*" " Pound must be in first column

syntax keyword iptablesTodo contained TODO FIXME XXX NOT NOTE

"============================================================================
" Section:  Autogenerated Groups  {{{2
"============================================================================

" Begin autogenerated section.
" iptables2vim: "iptables2vim,v 1.3 2005/12/15 00:26:55 ehaar Exp"
" iptables:     "iptables v1.3.0"

" Vim's built-in syntax highlighting for vim scripts can't handle keywords 
" with dashes very well, so the following section may look strange.
syntax keyword iptablesLongParam
   \ --add-set --ahspi --average --childlevel --clamp-mss-to-pmtu 
   \ --clustermac --cmd-owner --condition --connrate --continue --counter 
   \ --ctexpire --ctorigdst --ctorigsrc --ctproto --ctrepldst --ctreplsrc 
   \ --ctstate --ctstatus --datestart --datestop --days --del-set 
   \ --destination --destination-port --destination-ports --dport --dports 
   \ --dscp --dscp-class --dst --dst-range --dst-type --dstlimit 
   \ --dstlimit-burst --dstlimit-htable-expire --dstlimit-htable-gcinterval 
   \ --dstlimit-htable-max --dstlimit-htable-size --dstlimit-mode 
   \ --dstlimit-name --ecn-ip-ect --ecn-tcp-cwr --ecn-tcp-ece 
   \ --ecn-tcp-remove --espspi --every --exact --fragment --gid-owner --gw 
   \ --hash-init --hashmode --help --helper --icmp-type --iif --in-interface 
   \ --jump --length --limit --limit-burst --line-numbers --local-node 
   \ --log-ip-options --log-level --log-prefix --log-tcp-options 
   \ --log-tcp-sequence --log-uid --lower-limit --mac-source --mark 
   \ --modprobe --mss --new --numeric --oif --out-interface --packet 
   \ --physdev-in --physdev-is-bridged --physdev-is-in --physdev-is-out 
   \ --physdev-out --pid-owner --pkt-type --ports --protocol --realm 
   \ --reject-with --restore-mark --save-mark --set --set-class 
   \ --set-counters --set-dscp --set-dscp-class --set-mark --set-mss 
   \ --set-tos --sid-owner --source --source-port --source-ports --sport 
   \ --sports --src --src-range --src-type --start --state --syn --table 
   \ --tcp- --tcp-flags --tcp-option --tee --timestart --timestop --to 
   \ --to-destination --to-ports --to-source --tos --total-nodes --ttl-dec 
   \ --ttl-eq --ttl-gt --ttl-inc --ttl-lt --ttl-set --uid-owner 
   \ --ulog-cprange --ulog-nlgroup --ulog-prefix --ulog-qthreshold 
   \ --upper-limit --verbose
" End autogenerated section.

"============================================================================
" Section:  Group Linking  {{{1
"============================================================================

IptablesHiLink iptablesSaveDirective PreProc
IptablesHiLink iptablesSaveOperation PreProc

IptablesHiLink iptablesTable         Statement
IptablesHiLink iptablesTarget        Statement
IptablesHiLink iptablesBuiltinChain  Type

IptablesHiLink iptablesCommand       Operator

IptablesHiLink iptablesModuleName    Type
IptablesHiLink iptablesModuleType    Type

IptablesHiLink iptablesOperator      Operator
IptablesHiLink iptablesParam         Identifier
IptablesHiLink iptablesLongParam     Identifier

IptablesHiLink iptablesNumber        Constant

if b:iptablesSpecialDelimiters == 1
    IptablesHiLink iptablesMask      PreProc
    IptablesHiLink iptablesDelimiter Delimiter
else " b:iptablesSpecialDelimiters == 0 
    IptablesHiLink iptablesMask      Special
    IptablesHiLink iptablesDelimiter None
endif

IptablesHiLink iptablesString        Constant

IptablesHiLink iptablesComment       Comment
IptablesHiLink iptablesBadComment    Error
IptablesHiLink iptablesTodo          Todo   

"============================================================================
" Section:  Clean Up    {{{1
"============================================================================

delcommand IptablesHiLink

let b:current_syntax = "iptables"

if main_syntax == 'iptables'
  unlet main_syntax
endif

" Autoconfigure vim indentation settings
" vim:ts=4:sw=4:sts=4:fdm=marker
