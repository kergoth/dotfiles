function wrap_restoretitle () {
    for app in $argv; do
        eval "function $app () {
            setopt localtraps
            trap chpwd EXIT
            trap chpwd INT
            command $app \"\$@\"
        }"
    done
}
wrap_restoretitle vim ssh screen

function wrap_settitle () {
    for app in $argv; do
        eval "function $app () {
            setopt localtraps

            typeset \"oldtitle=\$overridetitle\"
            case \$TERM in
            (screen*)
                title $app
                ;;
            (*)
                title $app "\$@"
                ;;
            esac
            overridetitle=\"\$oldtitle\"

            trap chpwd EXIT
            trap chpwd INT
            command $app \"\$@\"
        }"
    done
}
wrap_settitle make ./configure tmake qmake scons cmake

overridetitle=
alias termtitle=title
title () {
    local newtitle="$*"
    if [[ -z $newtitle ]]; then
        newtitle="%~"
    fi
#     local hostinfo="%n@%m"
#     local hostsep=" - "
    local hostinfo="[%m - %n]"
    local hostsep=" "

    [[ -t 1 ]] || return

    overridetitle="$newtitle"
    case $TERM in
    (sun-cmd)
        if [[ -n $SSH_CLIENT ]]; then
            newtitle="$newtitle$hostsep$hostinfo"
        fi
        print -Pn "\e]l$newtitle\e\\"
        ;;
    (xterm*|rxvt*|(dt|k|E)term|putty*)
        if [[ -n $SSH_CLIENT ]]; then
            newtitle="$newtitle$hostsep$hostinfo"
        fi
        print -Pn "\e]2;$newtitle\a"
        ;;
    (screen*)
        local hardstatus
        if [[ -n $SSH_CLIENT ]]; then
            hardstatus="$hostinfo"
            if [[ -n $STY ]]; then
                hardstatus="${STY##*.}$hostsep$hardstatus"
            fi
        elif [[ -n "$STY" ]]; then
            hardstatus="${STY##*.}"
        fi

        if [[ -n $hardstatus ]]; then
            print -Pn "\e]0;$hardstatus\a"
        fi
        print -Pn "\ek$newtitle\e\\"
        ;;
    (*)
        overridetitle=
        ;;
    esac
}

resettermtitle () {
    overridetitle=
}

chpwd() {
    if [[ -n $overridetitle ]]; then
        termtitle $overridetitle
    else
        termtitle %~
        overridetitle=
    fi
}
chpwd

preexec() {
    PROMPTSCM="$(scm-prompt-output)"
}
preexec
