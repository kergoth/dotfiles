#!/usr/bin/zsh
#
# maxtime TIME COMMAND [ARGS ...]
# Run command as specified, until a max execution time of TIME.
# If the command is still running when TIME arrives, it will be killed.
#
# Most useful for reproducing problems where the success case results in
# continued execution, and the problem case interrupts that execution.
#
# Returns an exit code of 1 when the max time was hit, 0 otherwise.

setopt localoptions
setopt nomonitor

amount="$1"
shift

eval time ${=*} &
typeset timeproc=$!

sleep $amount &
typeset sleepproc=$!

function TRAPCHLD() {
    if [[ -e /proc/$timeproc ]]; then
        print "Max time of $amount seconds has arrived, killing process."
        kill $timeproc
    elif [[ -e /proc/$sleepproc ]]; then
        kill $sleepproc
    fi
}

wait $sleepproc
if [[ $? -eq 0 ]]; then
    return 1
else
    return 0
fi
