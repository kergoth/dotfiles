ZSH=$DOTFILESDIR/zsh

# Emacs mode
bindkey -e

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]; then
    source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($DOTFILESDIR/*/*.zsh)

# load the path files
source $ZSH/path.zsh
for file in ${(M)config_files:#*/path.zsh}; do
    if [[ $file = $ZSH/path.zsh ]]; then
        continue
    fi
    source $file
done

# pre-load plugin configuration
AUTOENV_AUTH_FILE=$XDG_DATA_HOME/autoenv_authorized

# Load "plugins"
for file in $ZSH/plugins/*/*.plugin.zsh(N) $ZSH/plugins/*/activate.sh(N) \
            ${(M)config_files:#*/*.plugin.zsh}; do
    case "$file" in
        */base16/*)
            continue
            ;;
        */syntax-highlighting/*)
            continue
            ;;
    esac
    source $file
done

# load everything but the path, plugin, and completion files
for file in ${${${${config_files:#*/path.zsh}:#*/completion.zsh}:#*/env.zsh}:#*/*.plugin.zsh}; do
    source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
# only check for new functions once a day
ZSH_COMPDUMP="$XDG_CACHE_HOME/zsh/zcompdump"
if [[ -n $ZSH_COMPDUMP(#qN.mh+24) ]]; then
    if [[ $PSEUDO_DISABLED = 0 ]]; then
        # When running under pseudo, we're actually still ourselves, so silently
        # ignore the compaudit.
        compinit -u -d $ZSH_COMPDUMP
    else
        compinit -d $ZSH_COMPDUMP
    fi
else
  compinit -C -d $ZSH_COMPDUMP
fi

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
    source $file
done

unset config_files

# Load manually loaded "plugins"
. $ZSH/plugins/bd/bd.zsh
. $ZSH/plugins/fzf/key-bindings.zsh
. $ZSH/plugins/fzf/completion.zsh
if [[ $OSTYPE =~ darwin ]]; then
    iterm2_hostname=$HOST
    . $ZSH/plugins/iterm2/iterm2.zsh
fi

if [[ "$PROFILE_STARTUP" == true ]]; then
    unsetopt xtrace
    exec 2>&3 3>&-
    rm -f $XDG_DATA_HOME/zsh/start.log
    ln -sf $XDG_DATA_HOME/zsh/start-$$.log $XDG_DATA_HOME/zsh/start.log
    if [[ "${PROFILE_STARTUP_PARSE:-true}" == true ]]; then
        $DOTFILESDIR/scripts/parse_zsh_startup $XDG_DATA_HOME/zsh/start.log | sort -nrk2 | less -RSXFE
        exit
    fi
fi
