ZSH=$DOTFILESDIR/zsh

# all of our zsh files
typeset -U config_files
config_files=($DOTFILESDIR/*/*.zsh)

# load the path files
source $ZSH/path.zsh
for file in ${(M)config_files:#*/path.zsh}; do
    if [[ $file = $ZSH/path.zsh ]]; then
        continue
    fi
    source $file
done

# Disable C-s (software flow control) on this tty
stty -ixon

# Freeze tty settings
ttyctl -f

for file in ${(M)config_files:#*/early.zsh}; do
    source $file
done

(( ${+commands[direnv]} )) && emulate zsh -c "$(direnv export zsh)"

# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

(( ${+commands[direnv]} )) && emulate zsh -c "$(direnv hook zsh)"

# Emacs mode
bindkey -e

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]; then
    source ~/.localrc
fi

# pre-load plugin configuration
AUTOENV_AUTH_FILE=$XDG_DATA_HOME/autoenv_authorized
if [[ -n $ITERM_PROFILE ]]; then
    if [[ $OSTYPE =~ darwin ]]; then
        iterm2_hostname=$HOST
    fi
else
    # Skip iterm2.zsh setup on non-iterm terminals
    ITERM_SHELL_INTEGRATION_INSTALLED=No
fi

# Load "plugins"
for file in $ZSH/plugins/*/*.plugin.zsh(N) $ZSH/plugins/*/activate.sh(N) \
            $DOTFILESDIR/*/zsh-plugins/*/*.plugin.zsh(N) \
            $DOTFILESDIR/*/zsh-plugins/*/activate.sh(N) \
            ${(M)config_files:#*/*.plugin.zsh}; do
    case "$file" in
        */base16/*)
            continue
            ;;
    esac
    source $file
done

# load everything but the path, plugin, and completion files
for file in ${${${${${config_files:#*/path.zsh}:#*/completion.zsh}:#*/env.zsh}:#*/*.plugin.zsh}:#*/final.zsh}; do
    source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
# only check for new functions once a day
ZSH_COMPDUMP="$XDG_CACHE_HOME/zsh/zcompdump-$ZSH_VERSION"
if [[ -n $ZSH_COMPDUMP(#qN.mh+24) ]]; then
    if [[ $PSEUDO_DISABLED = 0 ]]; then
        # When running under pseudo, we're actually still ourselves, so silently
        # ignore the compaudit.
        compinit -u -d $ZSH_COMPDUMP
    else
        compinit -d $ZSH_COMPDUMP
    fi
else
  compinit -C -d $ZSH_COMPDUMP
fi

# This is set here rather than config.zsh due to LS_COLORS being defined in
# ls/ls.zsh
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
    source $file
done

for file in ${(M)config_files:#*/final.zsh}; do
    source $file
done

unset config_files

# Load manually loaded "plugins"
. $ZSH/plugins/bd/bd.zsh

if [[ "$PROFILE_STARTUP_LINES" == true ]]; then
    unsetopt xtrace
    exec 2>&3 3>&-
    ln -sf $XDG_DATA_HOME/zsh/start-$$.log $XDG_DATA_HOME/zsh/start.log
    if [[ "${PROFILE_STARTUP_PARSE:-true}" == true ]]; then
        zsh_sort_timings $XDG_DATA_HOME/zsh/start.log | head -n 20
        exit
    fi
elif [[ "$PROFILE_STARTUP" == true ]]; then
    zprof
fi
